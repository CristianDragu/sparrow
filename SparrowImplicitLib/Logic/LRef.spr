package Logic;

import SprCore.StreamBasic;
import SL.Optional;
import SL.SharedPtr;
import SL.String;
import PrecedenceUtils;

public concept ValWrapper(x)
    if isValid(x.ValueType)
    //&& isValid(x.isSet())
    //&& isValid(x.get())
    //&& isValid(x())
    ;

public concept LRefType(x)
    if ValWrapper(x)
    && isValid(x.isLogicalRef)
    && isValid(x.reset())
    && isValid(x.reset(#$x.ValueType))
    ;


//public using logicDebug = 1;
//public using logicRefDebug = 1;


// A logical reference that contains strings
public using LStr = LRef(String);

// A logical reference that contains integers
public using LInt = LRef(Int);


/// A logical reference
///
/// A logical reference is in essence a shared pointer over an optional value.
/// Multiple logical variables might share the same value (or absence of a value). If a value is changed from one
/// reference, it's immediately visible in all the other references sharing the same value. This is particularly useful
/// in logical inference process, when we repeatedly assign values to variables and expect the values to be propagated
/// in all the places the variable appears.
public class LRef(valueType: Type)
{
    public using ValueType = valueType;
    public using ValuePassType = !@valueType;
    public using isLogicalRef = 1;

    public fun ctor
    {
        pptr ctor Optional(valueType)();
        if[ct] ( isValid(logicDebug) && isValid(logicRefDebug) )
            cout << "ctor: " << this << "\n";
    }

    public fun[convert] ctor(value: @ValueType)
    {
        pptr ctor Optional(valueType)(value);
        if[ct] ( isValid(logicDebug) && isValid(logicRefDebug) )
            cout << "value ctor: " << this << "\n";
    }

    public fun[convert] ctor(value: @AnyType) if isValid(ValueType(value))
    {
        pptr ctor Optional(valueType)(value);
        if[ct] ( isValid(logicDebug) && isValid(logicRefDebug) )
            cout << "value ctor: " << this << "\n";
    }

    public fun ctor(other: @LRef)
    {
        pptr ctor other.pptr;
        if[ct] ( isValid(logicDebug) && isValid(logicRefDebug) )
            cout << "copy ctor: " << this << "\n";
    }

    public fun dtor
    {
        if[ct] ( isValid(logicDebug) && isValid(logicRefDebug) )
            cout << "dtor: " << this << "\n";
    }


    public fun =(other: @LRef)
    {
        if ( other.isSet() )
            reset(other.get());
        else
            reset();
        if[ct] ( isValid(logicDebug) && isValid(logicRefDebug) )
            cout << "LRef.=" << this << "\n";
    }
    public fun =(value: @ValueType)
    {
        pptr.get().reset(value);
        if[ct] ( isValid(logicDebug) && isValid(logicRefDebug) )
            cout << "LRef.=" << this << "\n";
    }
    public fun =(value: AnyType) if isValid(ValueType(value))
    {
        pptr.get().reset(value);
        if[ct] ( isValid(logicDebug) && isValid(logicRefDebug) )
            cout << "LRef.=" << this << "\n";
    }


    public fun get: @ValueType         = pptr.get().get();

    public fun reset
    {
        // Reset the value shared by the shared pointer
        pptr.get().reset();
        if[ct] ( isValid(logicDebug) && isValid(logicRefDebug) )
            cout << "LRef.reset " << this << "\n";
    }

    public fun reset(value: @ValueType)
    {
        // Reset the value shared by the shared pointer
        pptr.get().reset(value);
        if[ct] ( isValid(logicDebug) && isValid(logicRefDebug) )
            cout << "LRef.reset " << this << "\n";
    }

    public fun isUnique                = pptr.isUnique();
    public fun useCount: UInt          = pptr.useCount();

    public fun isNull                  = pptr.get().isNull();
    public fun isSet                   = pptr.get().isSet();

    //public fun (): Bool                = true;
    
    public fun swap(other: @LRef)
    {
        pptr.swap(other.pptr);
    }

    public fun >>(os: @OutStream)
    {
        os << "LRef(" << mkStreamRefWrapper(pptr.get()) << ", " << useCount() << ", " << flush;
        if ( isSet() )
            os << get();
        else
            os << "null";
        os << ")" << flush;
    }
    private var pptr: SharedPtr(Optional(ValueType));
}

public fun mkLRef(x: @LRefType) = x;
public fun mkLRef(x: @StringRef) = LRef(String)(x);
public fun mkLRef(x: @AnyType): LRef(-@typeOf(x)) = x if !LRefType(x);

public fun mkValOrRef(x: @LRefType) = x;
public fun mkValOrRef(x: @ValWrapper) = x if !LRefType(typeOf(x));
public fun mkValOrRef(x: @StringRef) = Optional(String)(x);
public fun mkValOrRef(x: @AnyType) = Optional(-@typeOf(x))(x) if !ValWrapper(x);

public fun /+/ (l, r: @AnyType) = Impl.mkPlusOp(mkValOrRef(l), mkValOrRef(r));
public fun /-/ (l, r: @AnyType) = Impl.mkMinusOp(mkValOrRef(l), mkValOrRef(r));
public fun |*| (l, r: @AnyType) = Impl.mkMulOp(mkValOrRef(l), mkValOrRef(r));
public fun |/| (l, r: @AnyType) = Impl.mkDivOp(mkValOrRef(l), mkValOrRef(r));

setOperPrecedence("/+/", getOperPrecedence("+") + 1);
setOperPrecedence("/-/", getOperPrecedence("-") + 1);
setOperPrecedence("|*|", getOperPrecedence("*") + 1);
setOperPrecedence("|/|", getOperPrecedence("/") + 1);

package Impl
{
    class BinaryOp(leftType, rightType, operType: Type) if ValWrapper(#$leftType) && ValWrapper(#$rightType)
    {
        using ValueType = leftType.ValueType;

        fun ctor(l: @leftType, r: @rightType, oper: operType)
        {
            this.left ctor l;
            this.right ctor r;
            this.oper ctor oper;
        }

        fun get: ValueType          = oper(left.get(), right.get());

        fun isNull                  = left.isNull() || right.isNull();
        fun isSet                   = left.isSet() && right.isSet();

        fun >>(os: @OutStream)
        {
            os << "BinaryOp(left=" << left << ", right=" << right << ", " << TypeOp.description(operType) << ")" << flush;
        }

        private var left: leftType;
        private var right: rightType;
        private var oper: operType;
    }

    fun mkBinaryOp(l, r: @ValWrapper, oper: @AnyType) = BinaryOp(-@typeOf(l), -@typeOf(r), -@typeOf(oper))(l, r, oper);


    fun mkPlusOp(l, r: @ValWrapper) = mkBinaryOp(l, r, Plus(l.ValueType, r.ValueType)());
    fun mkMinusOp(l, r: @ValWrapper) = mkBinaryOp(l, r, Minus(l.ValueType, r.ValueType)());
    fun mkMulOp(l, r: @ValWrapper) = mkBinaryOp(l, r, Mul(l.ValueType, r.ValueType)());
    fun mkDivOp(l, r: @ValWrapper) = mkBinaryOp(l, r, Div(l.ValueType, r.ValueType)());


    class Plus(lt, rt: Type)
    {
        fun ()(l: lt, r: rt) = l + r;
    }
    class Minus(lt, rt: Type)
    {
        fun ()(l: lt, r: rt) = l - r;
    }
    class Mul(lt, rt: Type)
    {
        fun ()(l: lt, r: rt) = l * r;
    }
    class Div(lt, rt: Type)
    {
        fun ()(l: lt, r: rt) = l / r;
    }
}
