public using LLocation = Location;

public class[rtct] Location
{
    public var sourceCodePlaceholder: @Int;
    public var startLineNo: UInt;
    public var startColNo: UInt;
    public var endLineNo: UInt;
    public var endColNo: UInt;

    public fun ctor
    {
        sourceCodePlaceholder := null;
        startLineNo ctor 0;
        endLineNo ctor 0;
        startColNo ctor 0;
        endColNo ctor 0;
    }

    public fun ctor(startLineNo, endLineNo, startColNo, endColNo: UInt)
    {
        this.sourceCodePlaceholder := null;
        this.startLineNo ctor startLineNo;
        this.endLineNo ctor endLineNo;
        this.startColNo ctor startColNo;
        this.endColNo ctor endColNo;
    }

    public fun[ct, convert] ctor(loc: Meta.Location)
    {
        this.sourceCodePlaceholder := loc.sourceCode.impl;
        this.startLineNo ctor loc.startLineNo;
        this.endLineNo ctor loc.endLineNo;
        this.startColNo ctor loc.startColNo;
        this.endColNo ctor loc.endColNo;
    }

    public fun[ct] asMetaLocation: Meta.Location
    {
        var res: Meta.Location;
        res.sourceCode.impl := sourceCodePlaceholder;
        res.startLineNo = startLineNo;
        res.endLineNo = endLineNo;
        res.startColNo = startColNo;
        res.endColNo = endColNo;
        return res;
    }

    /// Set the location to (1,1,1,1), the first character in a stream
    public fun setOne
    {
        startLineNo = 1;
        startColNo = 1;
        endLineNo = 1;
        endColNo = 1;
    }

    /// Make the start position to be the same as the end position
    public fun stepOver
    {
        startLineNo = endLineNo;
        startColNo = endColNo;
    }

    /// Add the given number of columns to the end position; start position remains unchanged
    public fun addColumns(count: UInt)
    {
        endColNo += count;
    }

    /// Add the given number of lines to the end position; start position remains unchanged
    public fun addLines(count: UInt)
    {
        endLineNo += count;
        endColNo = 1;
    }

    /// Set the start position of this location to the start position of the given location; the end position remains unchanged
    public fun copyStart(other: @Location)
    {
        startLineNo = other.startLineNo;
        startColNo = other.startColNo;
    }

    /// Set the end position of this location to the end position of the given location; the start position remains unchanged
    public fun copyEnd(other: @Location)
    {
        endLineNo = other.endLineNo;
        endColNo = other.endColNo;
    }

    /// Set this location with both start and end to equal the start of the given location
    public fun setAsStartOf(other: @Location)
    {
        startLineNo = other.startLineNo;
        startColNo = other.startColNo;
        endLineNo = other.startLineNo;
        endColNo = other.startColNo;
    }

    /// Set this location with both start and end to equal the end of the given location
    public fun setAsEndOf(other: @Location)
    {
        startLineNo = other.endLineNo;
        startColNo = other.endColNo;
        endLineNo = other.endLineNo;
        endColNo = other.endColNo;
    }
}

