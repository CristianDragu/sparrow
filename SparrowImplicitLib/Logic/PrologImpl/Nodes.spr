import Token;
import SL.Vector;
import SL.SharedPtr;

public using PrologProgramPtr = SharedPtr(PrologProgram);
public using PredicateClausePtr = SharedPtr(PredicateClause);
public using ConditionPtr = SharedPtr(Condition);
public using ExpressionPtr = SharedPtr(Expression);

public class[ct] PrologProgram
{
    public var predicates: Vector(PredicateClausePtr);
    public var loc: Location;

    public fun >>(os: @OutStream)
    {
        for ( p = predicates.all() )
            os << p << endl;
    }
}

public class[ct] PredicateClause
{
    public var name: String;
    public var args: Vector(ExpressionPtr);
    public var conditions: Vector(ConditionPtr);
    public var loc: Location;

    public fun >>(os: @OutStream)
    {
        os << name << "(";
        for ( i = 0..args.size() )
        {
            if ( i>0 )
                os << ", ";
            os << args(i).get();
        }
        os << ") :- ";
        for ( i = 0..conditions.size() )
        {
            if ( i>0 )
                os << ", ";
            os << conditions(i).get();
        }
        os << ".";
    }
}

public class[ct] Condition
{
    public var kind: Int;
    public var loc: Location;
    public var args: Vector(SharedPtr(Expression));
    public var predName: String;

    public fun ctor(loc: @Location, predName: @String, args: @Vector(SharedPtr(Expression)))
    {
        this.kind ctor ckPredCall;
        this.loc ctor loc;
        this.predName ctor predName;
        this.args ctor args;
    }

    public fun ctor(loc: @Location, isLeft, isRight: @SharedPtr(Expression))
    {
        this.kind ctor ckIsExpr;
        this.loc ctor loc;
        this.predName ctor;
        this.args ctor;
        this.args.reserve(2);
        this.args.pushBack(isLeft);
        this.args.pushBack(isRight);
    }

    public fun >>(os: @OutStream)
    {
        if ( kind == ckPredCall )
        {
            os << predName << '(';
            for ( i = 0..args.size() )
            {
                if ( i>0 )
                    os << ", ";
                os << args(i).get();
            }
            os << ')';
        }
        else
            os << args(0).get() << " is " << args(1).get();
    }
}

public class[ct] Expression
{
    public var kind: Int;
    public var loc: Location;
    public var data: String;
    public var left, right: SharedPtr(Expression);
    public var typeSetIdx: Int;
    public var paramIdx: Int;

    public fun ctor(kind: Int, loc: @Location)
    {
        this.kind ctor kind;
        this.loc ctor loc;
        this.data ctor;
        this.left ctor;
        this.right ctor;
        this.typeSetIdx ctor -1;
        this.paramIdx ctor -1;
    }

    public fun ctor(kind: Int, loc: @Location, data: @String)
    {
        this.kind ctor kind;
        this.loc ctor loc;
        this.data ctor data;
        this.left ctor;
        this.right ctor;
        this.typeSetIdx ctor -1;
        this.paramIdx ctor -1;
    }

    public fun ctor(kind: Int, loc: @Location, left, right: @SharedPtr(Expression))
    {
        this.kind ctor kind;
        this.loc ctor loc;
        this.data ctor;
        this.left ctor left;
        this.right ctor right;
        this.typeSetIdx ctor -1;
        this.paramIdx ctor -1;
    }

    public fun >>(os: @OutStream)
    {
        if ( kind == ekAtom )
            os << data;
        else if ( kind == ekNumber )
            os << data;
        else if ( kind == ekVar )
            os << "var<" << data << '>';
        else if ( kind == ekAntiQuote )
            os << "aq<" << data << '>';
        else if ( kind == ekOpPlus )
            os << '(' << left.get() << '+' << right.get() << ')';
        else if ( kind == ekOpMinus )
            os << '(' << left.get() << '-' << right.get() << ')';
        else if ( kind == ekOpMul )
            os << '(' << left.get() << '*' << right.get() << ')';
        else if ( kind == ekOpDiv )
            os << '(' << left.get() << '/' << right.get() << ')';
        else
            os << "<invalid expression kind:" << kind << ">";
    }
}

public using ekAtom = 0;
public using ekNumber = 1;
public using ekVar = 2;
public using ekAntiQuote = 3;
public using ekOpPlus = 4;
public using ekOpMinus = 5;
public using ekOpMul = 6;
public using ekOpDiv = 7;

public using ckPredCall = 8;
public using ckIsExpr = 9;

public fun[ct] mkAtom(loc: @Location, atom: @String): ExpressionPtr = ExpressionPtr.create(ekAtom, loc, atom);
public fun[ct] mkNumber(loc: @Location, data: @String): ExpressionPtr = ExpressionPtr.create(ekNumber, loc, data);
public fun[ct] mkVar(loc: @Location, varName: @String): ExpressionPtr = ExpressionPtr.create(ekVar, loc, varName);
public fun[ct] mkAntiQuote(loc: @Location, aq: @String): ExpressionPtr = ExpressionPtr.create(ekAntiQuote, loc, aq);

public fun[ct] mkOpPlus(loc: @Location, l, r: @ExpressionPtr): ExpressionPtr = ExpressionPtr.create(ekOpPlus, loc, l, r);
public fun[ct] mkOpMinus(loc: @Location, l, r: @ExpressionPtr): ExpressionPtr = ExpressionPtr.create(ekOpMinus, loc, l, r);
public fun[ct] mkOpMul(loc: @Location, l, r: @ExpressionPtr): ExpressionPtr = ExpressionPtr.create(ekOpMul, loc, l, r);
public fun[ct] mkOpDiv(loc: @Location, l, r: @ExpressionPtr): ExpressionPtr = ExpressionPtr.create(ekOpDiv, loc, l, r);

public fun[ct] mkPredCall(loc: @Location, predName: @String, args: @Vector(ExpressionPtr)): ConditionPtr = ConditionPtr.create(loc, predName, args);
public fun[ct] mkIsExpr(loc: @Location, l, r: @ExpressionPtr): ConditionPtr = ConditionPtr.create(loc, l, r);
