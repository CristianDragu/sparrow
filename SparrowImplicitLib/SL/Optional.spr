/// A container that may or may not store a value.
/// One can check whether the value is defined or not, and can access the value
class Optional(valueType: Type)
{
    using ValueType = valueType;
    
    fun[noDefault] ctor
    {
        constructed ctor false;
        value ctor Uninitialized();
    }
    fun[noDefault] ctor(other: @Optional)
    {
        constructed ctor other.constructed;
        if ( other.constructed )
            value ctor other.value;
    }
    fun[noDefault] ctor(val: @ValueType)
    {
        constructed ctor true;
        value ctor val;
    }
    fun[noDefault] dtor
    {
        reset();
    }

    fun isNull                                      = !constructed;
    fun isSet                                       = constructed;
    fun get: @ValueType                             = value;
    fun getOrElse(default: @ValueType): @ValueType  = ife(constructed, cast(@ValueType, value), default);
    
    fun reset
    {
        if ( constructed )
            value dtor;
        constructed = false;
    }
    fun reset(val: @ValueType)
    {
        if ( constructed )
            value dtor;
        value ctor val;
        constructed = true;
    }
    
    fun swap(other: @Optional)
    {
        var tmp = this;
        this = other;
        other = this;
    }
    
    fun =(other: @Optional)     { if ( other.constructed ) reset(other.value); else reset(); }
    fun =(val: @ValueType)      { reset(val); }
    
    fun ==(other: Optional)     = constructed == other.constructed && (!constructed || value == other.value);

    fun >>(os: @OutStream) if isValid(os << #$ValueType)
    {
        if ( isSet() )
            os << "Optional(" << value << ")";
        else
            os << "Optional(none)";
        os << flush;
    }

    private var value: ValueType;
    private var constructed: Bool;
}
