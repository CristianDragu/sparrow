
public class[noDefault] ScopedPtr(type: Type)
{
    public using ValueType = type;

    public fun ctor()
    {
        ref := null;
    }

    public fun ctor(ref: @ValueType)
    {
        this.ref := ref;
    }

    public fun dtor()
    {
        delete(ref);
    }

    public fun get: @ValueType         = ref;
    public fun isNull                  = ref === null;
    public fun isSet                   = ref !== null;

    public fun ==(other: ScopedPtr)    = ref === other.ref;

    public fun reset                   { delete(ref); ref := null; }
    public fun reset(ref: @ValueType)  { delete(this.ref); this.ref := ref; }

    public fun swap(other: @ScopedPtr)
    {
        var tmp: @ValueType = other.ref;

        other.ref := ref;
        ref := tmp;
    }

    private var ref: @ValueType;
}
