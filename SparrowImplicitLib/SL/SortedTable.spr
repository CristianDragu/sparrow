import Array;
import StaticArray;
import Vector;
import Algorithms;
import DefaultHashFunction;

public class SortedTable(keyType, valueType, valueToKeyType, lessType, compareType: Type)
{
    public using KeyType = keyType;
    public using ValueType = valueType;
    public using ValueToKeyType = valueToKeyType;
    public using LessType = lessType;
    public using CompareType = compareType;
    public using RangeType = Vector(valueType).RangeType;

    public fun ctor()
    {
        this.ctor(0, LessType(), CompareType());
    }

    public fun ctor(n: SizeType, less: LessType, comp: CompareType)
    {
        table.ctor();
        this.valToKey ctor valToKey;
        this.less ctor less;
        this.comp ctor comp;
        if ( n > 0)
            reserve(n);
    }

    public fun ctor(range: AnyType, n: SizeType, less: LessType, comp: CompareType)
    {
        table.ctor();
        if ( n > 0 )
            table.reserve(n);
        else
            table.reserve(rangeSize(range));
        this.valToKey ctor valToKey;
        this.less ctor less;
        this.comp ctor comp;

        //table.assign(range);
        //sort(table.all(), less);
        for ( v = range )
            insert(v);
    }

    public fun ctor(other: @SortedTable)
    {
        table ctor other.table;
        less ctor other.less;
        comp ctor other.comp;
    }

    public fun swap(other: @SortedTable)
    {
        table.swap(other.table);

        var tmpv = valToKey;
        valToKey = other.valToKey;
        other.valToKey = tmpv;

        var tmpl = less;
        less = other.less;
        other.less = tmpl;

        var tmpc = comp;
        comp = other.comp;
        other.comp = tmpc;
    }

    public fun lessFunction    = less;
    public fun comparator      = comp;

    public fun isEmpty         = table.isEmpty();
    public fun size            = table.size();

    public fun all             = table.all();

    public fun reserve(n: SizeType)    { table.reserve(n); }

    public fun insert(value: @ValueType): RangeType
    {
        var r = lowerBound(valToKey(value));
        var idx = table.size() - r.size();
        if ( r.isEmpty() || !comp(valToKey(value), valToKey(r.front())) )
            table.insertBefore(value, r);
        var res = table.all();
        res.popFront(idx);
        return res;
    }

    public fun insert(range: Range)
    {
        table.reserve(table.size() + rangeSize(range));
        for ( v = range )
        {
            var r = lowerBound(valToKey(v));
            if ( r.isEmpty() || !comp(valToKey(v), valToKey(r.front())) )
                table.insertBefore(v, r);
        }
    }

    public fun remove(key: @KeyType)
    {
        if ( isEmpty() )
            return;

        var r = find(key);
        if ( !r.isEmpty() )
        {
            var idx = r.frontPtr().diff(table.all().frontPtr());
            table.remove(idx);
        }
    }

    public fun remove(range: RangeType)
    {
        if ( !range.isEmpty() )
            table.remove(range);
    }

    public fun contains(key: @KeyType): Bool  = !find(key).isEmpty();
    public fun count(key: @KeyType): SizeType = ife(find(key).isEmpty(), 0, 1);

    public fun lowerBound(key: @KeyType): RangeType
    {
        var first: SizeType = 0;
        var len = table.size();
        while ( len > 0 )
        {
            var l2 = len / 2;
            var el: @ValueType = table(first + l2);
            if ( less(valToKey(el), key) )
            {
                first += l2+1;
                len -= l2+1;
            }
            else
                len = l2;
        }
        var r = table.all();
        r.popFront(first);
        return r;
    }

    public fun upperBound(key: @KeyType): RangeType
    {
        var first: SizeType = 0;
        var len = table.size();
        while ( len > 0 )
        {
            var l2 = len / 2;
            var el: @ValueType = table(first + l2);
            if ( less(key, valToKey(el)) )
                len = l2;
            else
            {
                first += l2+1;
                len -= l2+1;
            }
        }
        var r = table.all();
        r.popFront(first);
        return r;
    }

    public fun equalRange(key: @KeyType): RangeType
    {
        var r = find(key);
        if ( !r.isEmpty() )
            return RangeType(r.frontPtr(), r.frontPtr().advance(1));
        else
            return RangeType();
    }

    public fun find(key: @KeyType): RangeType
    {
        var r = lowerBound(key);
        if ( !r.isEmpty() && comp(key, valToKey(r.front())) )
            return r;
        else
            return RangeType();
    }

    public fun clear()
    {
        table.clear();
    }

    public fun =(other: @SortedTable): @SortedTable
    {
        table = other.table;
        valToKey = other.valToKey;
        comp = other.comp;
        less = other.less;
        valToKey = other.valToKey;

        return this;
    }

    public fun ==(other: @SortedTable): Bool
    {
        return valToKey == other.valToKey && less == other.less && comp == other.comp && table == other.table;
    }

    public fun !=(other: @SortedTable): Bool = !(this == other);

    private var table: Vector(ValueType);
    private var valToKey: ValueToKeyType;
    private var less: LessType;
    private var comp: CompareType;
}

public class[rtct] Less(type: Type) if isValid(#$type < #$type)
{
    public fun ()(l, r: @type) = l < r;
}

