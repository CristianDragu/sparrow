import os;

import meta.astNodes;

//using debugMode = true;


if[ct] ( isValidAndTrue(debugMode) )
fun[rtct, macro] assert(cond: meta.AstNode): meta.AstNode
{
    using meta.*;
    using meta.SprFrontend.*;
    using meta.Feather.*;

    var loc = cond.location();
    var filename = loc.sourceCode.filename();
    var lineNum = loc.startLineNo;
    var args = mkNodeList(loc);
    addToNodeList(args, mkStringLiteral(loc, loc.getCorrespondingCode()));
    addToNodeList(args, mkStringLiteral(loc, filename));
    addToNodeList(args, mkIntLiteral(loc, lineNum));
    var failureCall = mkFunApplication(loc, mkIdentifier(loc, "assertionFailure"), args);
    return mkIf(loc, mkPrefixOp(loc, "!", mkParenthesisExp(loc, cond)), failureCall, AstNode());
}
else
fun[rtct, macro] assert(cond: meta.AstNode): meta.AstNode
{
    return meta.Feather.mkNop(cond.location());
}

if[ct] ( isValidAndTrue(debugMode) )
fun[rtct, macro] verify(cond: meta.AstNode): meta.AstNode
{
    using meta.*;
    using meta.SprFrontend.*;
    using meta.Feather.*;

    var loc = cond.location();
    var filename = loc.sourceCode.filename();
    var lineNum = loc.startLineNo;
    var args = mkNodeList(loc);
    addToNodeList(args, mkStringLiteral(loc, loc.getCorrespondingCode()));
    addToNodeList(args, mkStringLiteral(loc, filename));
    addToNodeList(args, mkIntLiteral(loc, lineNum));
    var failureCall = mkFunApplication(loc, mkIdentifier(loc, "assertionFailure"), args);
    return mkIf(loc, mkPrefixOp(loc, "!", mkParenthesisExp(loc, cond)), failureCall, AstNode());
}
else
fun[rtct, macro] verify(cond: meta.AstNode): meta.AstNode
{
    return cond;
}

fun[rtct] assertionFailure(message: StringRef, filename: StringRef, lineNum: UInt)
{
    cout << "Assertion failure: " << message << endl;
    if ( !filename.isEmpty() )
    {
        cout << "at " << filename << " line " << lineNum;
    }
    cout << flush << endl;
    os.exit(-1);
}

