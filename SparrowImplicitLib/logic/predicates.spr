module logic.predicates;

public import lRef;
import std.function;

/// Concept describing the type of a predicate: anything that can be called and evaluates to Bool
concept PredicateType(x)
    if typeOf(x()) == Bool;

/// Basic implementation of a predicate with type erasure
/// We store the actual predicate function as a Function object that takes no parameters and returns Bool
/// Provides implicit conversion from any predicate
class Predicate
{
    using arity = 0;

    fun[convert] ctor(pred: @PredicateType)
    {
        predObj ctor pred;
    }
    fun isNull = predObj.isNull();
    fun isSet = predObj.isSet();

    fun (): Bool = predObj();

    fun >>(os: @OutStream) { os << "Pred-"<<predObj; }
    var predObj: Function(Bool);
}


/// Logic equality: equality test + inference
fun eq(l, r: @AnyType) = Impl.mkEq(mkValOrRef(l), mkValOrRef(r));
fun /=/(l, r: @AnyType) = Impl.mkEq(mkValOrRef(l), mkValOrRef(r));

// Assignment: used for native functions instead of unification
fun =/(out: @AnyType, in: AnyType): Bool if -@typeOf(out) == -@typeOf(in)
{
    out = in;
    return true;
}

setOperPrecedence("/=/", getOperPrecedence("==") + 1);
setOperPrecedence("=/", getOperPrecedence("==") + 1);



/// Logic Disjunction
fun[rtct] ||(l, r: @PredicateType) = OrRelation(-@typeOf(l), -@typeOf(r))(l, r);

/// Logic Conjunction
fun[rtct] &&(l, r: @PredicateType) = AndRelation(-@typeOf(l), -@typeOf(r))(l, r);

/// Relation that always returns false
fun mkFalse = LFalse();
/// Relation that always returns true
fun mkTrue = LTrue();


class UnifyLR(leftType, rightType: Type) if ValWrapper(#$leftType) && ValWrapper(#$rightType)
{
    using ValueType = Bool;
    using ValuePassType = Bool;

    fun ctor(l: @leftType, r: @rightType)
    {
        this.leftVal ctor l;
        this.rightVal ctor r;
        coEntryPt ctor 0;
        if[ct] ( isValid(logicDebug) )
            cout << "UnifyLR.ctor: " << this << "\n";
    }

    fun ctor(other: @UnifyLR)
    {
        this.leftVal ctor other.leftVal;
        this.rightVal ctor other.rightVal;
        this.coEntryPt ctor other.coEntryPt;
        if[ct] ( isValid(logicDebug) )
            cout << "UnifyLR.copy ctor: " << this << "\n";
    }

    fun dtor
    {
        if[ct] ( isValid(logicDebug) )
            cout << "UnifyLR.dtor: " << this << "\n";
    }

    fun (): Bool
    {
        //cout << "UnifyLR.(): ltype=" << TypeOp.description(leftType) << ", rtype=" << TypeOp.description(rightType) << "\n" << flush;
        if[ct] ( isValid(logicDebug) )
            cout << "UnifyLR.(): " << this << "\n";
        if ( coEntryPt == 0 )   // Never called before
        {
            if ( leftVal.isNull() && rightVal.isNull() )
            {
                // Cannot unify two undefined references
                coEntryPt = -1;
                return false;
            }
            if ( leftVal isSet )
            {
                if ( rightVal isSet )
                {
                    coEntryPt = -1; // Nothing more to do here
                    if[ct] ( isValid(logicDebug) )
                        cout << "UnifyLR.(), checking equality: " << leftVal << "==" << rightVal << "\n";
                    var res = leftVal.get() == rightVal.get();
                    return res;
                }
                if[ct] ( LRefType(#$rightType) && isValid(rightVal = leftVal.get()) )
                {
                    rightVal = leftVal.get();
                    coEntryPt = 1;
                    if[ct] ( isValid(logicDebug) )
                        cout << "Unify, assigning " << leftVal.get() << " to right side => " << this << endl;
                    return true;
                }
                coEntryPt = -1;
                return false;
            }
            else
            {
                if[ct] ( LRefType(#$leftType) && isValid(leftVal = rightVal.get()) )
                {
                    leftVal = rightVal.get();
                    coEntryPt = 2;
                    if[ct] ( isValid(logicDebug) )
                        cout << "Unify, assigning " << rightVal.get() << " to left side => " << this << endl;
                    return true;
                }
                coEntryPt = -1;
                return false;
            }
        }
        else if ( coEntryPt == 1 )  // After we write into the right value
        {
            if[ct] ( isValid(logicDebug) )
                cout << "Unify, no more values (after right side assign)\n";
            if[ct] ( LRefType(#$rightType) )
                rightVal reset;
            coEntryPt = -1;  // Nothing more to do here
            return false;
        }
        else if ( coEntryPt == 2 )  // After we write into the left value
        {
            if[ct] ( isValid(logicDebug) )
                cout << "Unify, no more values (after left side assign)\n";
            if[ct] ( LRefType(#$leftType) )
                leftVal reset;
            coEntryPt = -1;  // Nothing more to do here
            return false;
        }
        else
        {
            if[ct] ( isValid(logicDebug) )
                cout << "Unify, done\n";
            return false;
        }
    }

    fun >>(os: @OutStream)
    {
        os << "UnifyLR(left=" << leftVal << ", right=" << rightVal << ", " << coEntryPt << ")" << flush;
    }

    private var coEntryPt: Int;
    private var leftVal: leftType;
    private var rightVal: rightType;
}

class[rtct] OrRelation(leftPredicateType, rightPredicateType: Type)
{
    using ValueType = Bool;
    using ValuePassType = Bool;

    fun ctor(l: @leftPredicateType, r: @rightPredicateType)
    {
        this.left ctor l;
        this.right ctor r;
        coEntryPt ctor 0;
        if[ct] ( isValid(logicDebug) )
            cout << "OrRelation.ctor: " << this << "\n";
    }

    fun dtor
    {
        if[ct] ( isValid(logicDebug) )
            cout << "OrRelation.dtor: " << this << "\n";
    }

    fun (): Bool
    {
        if[ct] ( isValid(logicDebug) )
            cout << "OrRelation.(): " << this << "\n";
        if ( coEntryPt == 0 )   // Never called before, or left is returning true
        {
            // While left returns true, stay in the same state and return true
            if ( left() )
                return true;

            // Left is not false, check right
            if ( right() )
            {
                // If the right is true, go to state 1 and return true
                coEntryPt = 1;
                return true;
            }
            coEntryPt = -1;
            return false;
        }
        else if ( coEntryPt == 1 )  // Right is returning true
        {
            // While right returns true, stay in the same state and return true
            if ( right() )
                return true;

            // Right is false, nothing to do anymore
            coEntryPt = -1; // Done here
        }
        return false;
    }

    fun >>(os: @OutStream)
    {
        os << "OrRelation(left=" << left << ", right=" << right << ", " << coEntryPt << ")" << flush;
    }

    private var coEntryPt: Int;
    private var left: leftPredicateType;
    private var right: rightPredicateType;
}

class[rtct] AndRelation(leftPredicateType, rightPredicateType: Type)
{
    using ValueType = Bool;
    using ValuePassType = Bool;

    fun ctor(l: leftPredicateType, r: rightPredicateType)
    {
        this.left ctor l;
        this.right ctor r;
        this.rightBegin ctor r;
        coEntryPt ctor 0;
        if[ct] ( isValid(logicDebug) )
            cout << "AndRelation.ctor: " << this << "\n";
    }

    fun (): Bool
    {
        if[ct] ( isValid(logicDebug) )
            cout << "AndRelation.(): " << this << "\n";
        if ( coEntryPt == 0 || coEntryPt == 1 )   // Never called before; stay here until we have a positive result
        {
            var skipEvalLeft = coEntryPt == 1;
            while ( skipEvalLeft || left() )
            {
                // Left is (still) true; check if right is also true
                if ( right() )
                {
                    // Both left and right are true
                    coEntryPt = 1;
                    return true;
                }

                // Right is false now; reset right and check left for new values
                right = rightBegin;
                skipEvalLeft = false;
            }

            // Left is now false, bail out
            coEntryPt = -1;
            return false;
        }
        return false;
    }

    fun >>(os: @OutStream)
    {
        os << "AndRelation(left=" << left << ", right=" << right << ", " << coEntryPt << ")" << flush;
    }

    private var coEntryPt: Int;
    private var left: leftPredicateType;
    private var right, rightBegin: rightPredicateType;
}

class LFalse
{
    fun () = false;

    fun >>(os: @OutStream)
    {
        os << "LFalse" << flush;
    }
}

class LTrue
{
    fun () = true;

    fun >>(os: @OutStream)
    {
        os << "LTrue" << flush;
    }
}

package Impl
{
    fun mkEq(l, r: @ValWrapper) = UnifyLR(-@typeOf(l), -@typeOf(r))(l, r);
}

