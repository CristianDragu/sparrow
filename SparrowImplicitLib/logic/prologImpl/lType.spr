import nodes;
import bitOper;
import metadiag = meta.diagnostic;

class[ct] LType
{
    using atom = 1;
    using number = 2;
    //using input = 4;
    //using output = 8;
    using initialVal = atom !|! number;// !|! input !|! output;

    private var bits: Int;

    fun ctor
    {
        bits ctor initialVal;
    }

    fun isValid: Bool       = (testBit(atom) || testBit(number));// && (testBit(input) || testBit(output));

    fun isAtom: Bool        = testBit(atom);
    fun isNumber: Bool      = testBit(number);
    fun isAtomOnly: Bool    = testBit(atom) && !testBit(number);
    fun isNumberOnly: Bool  = testBit(number) && !testBit(atom);


    fun setAtomOnly: Bool   = setAlternativeBits(atom, number);
    fun setNumberOnly: Bool = setAlternativeBits(number, atom);

    //fun setInputOnly: Bool  = setAlternativeBits(input, output);
    //fun setOutputOnly: Bool = setAlternativeBits(output, input);

    private fun testBit(b: Int): Bool = ((bits !&! b) != 0);

    private fun setAlternativeBits(toSet, toClear: Int): Bool
    {
        if ( !testBit(toSet) )
            return false;
        if ( testBit(toClear) )
            bits -= toClear;
        return true;
    }
}

/// Structure indicating of set of expressions that share the same type
class[ct] LTypeSet
{
    var type: LType;                    ///< The type of all the expressions in this set
    var expList: Vector(ExpressionPtr); ///< The list of all the expressions in this set, that share the same type
    var idx: Int;                       ///< The index of this type set

    fun ctor(idx: Int, type: LType)
    {
        this.type ctor type;
        this.expList ctor;
        this.idx ctor idx;
    }

    fun addExpression(exp: @ExpressionPtr)
    {
        expList.pushBack(exp);
        exp.get().typeSetIdx = idx;
    }

    fun setAtomOnly(loc: @Location)
    {
        if ( !type.setAtomOnly() )
            reportTypeError(loc);
    }

    fun setNumberOnly(loc: @Location)
    {
        if ( !type.setNumberOnly() )
            reportTypeError(loc);
    }

    fun mergeWith(other: @LTypeSet, mergeLocation: @Location)
    {
        if ( this === other )
            return;

        //cout << '{' << idx << "} + {" << other.idx << "} -- <" << this << "> ++++ <" << other << ">" << flush;

        // Merge the two lists
        //expList.insertAfter(other.expList.all(), expList.all());
        expList.reserve(expList.size() + other.expList.size());
        for ( e = other.expList.all() )
        {
            expList.pushBack(e);
            e.get().typeSetIdx = idx;
        }
        other.expList.clear();

        // Compute merged type
        if ( other.type.isAtomOnly() )
        {
            if ( !type.setAtomOnly() )
                reportTypeError(mergeLocation);
        }
        else if ( other.type.isNumberOnly() )
        {
            if ( !type.setNumberOnly() )
                reportTypeError(mergeLocation);
        }

        other.type = LType();

        //cout << " => " << this << endl << flush;
    }

    fun >>(os: @OutStream)
    {
        os << '{' << idx << "} ";
        if ( type.isAtomOnly() )
            os << "str";
        else if ( type.isNumberOnly() )
            os << "int";
        else
            os << "???";
        os << " :";
        for ( pe = expList.all() )
        {
            os << ' ' << pe.get();
        }
    }

    private fun reportTypeError(loc: @Location)
    {
        var diags: Vector(metadiag.Diagnostic);
        var l = loc.asMetaLocation();
        diags.pushBack(metadiag.Diagnostic("Type mismatch: cannot combine atom with number", l, metadiag.diagError));

        //writeLoc(loc);
        //cout << "ERROR: Type mismatch: cannot combine atom with number" << endl;

        var atomExp = findAtomExp();
        if ( atomExp isSet )
        {
            var lAtom = atomExp.get().loc.asMetaLocation();
            diags.pushBack(metadiag.Diagnostic("See also atom expression:", lAtom, metadiag.diagInfo));

            //writeLoc(atomExp.get().loc);
            //cout << "See atom expression" << endl;
        }

        var numExp = findNumberExp();
        if ( numExp isSet )
        {
            var lNum = numExp.get().loc.asMetaLocation();
            diags.pushBack(metadiag.Diagnostic("See also number expression:", lNum, metadiag.diagInfo));

            //writeLoc(numExp.get().loc);
            //cout << "See numeric expression" << endl;
        }

        metadiag.report(diags);

        //os.exit(-1);
    }

    private fun findNumberExp: ExpressionPtr
    {
        for ( e = expList.all() )
        {
            var k = e.get().kind;
            if ( k == ekNumber || k == ekOpPlus || k == ekOpMinus || k == ekOpMul || k == ekOpDiv )
                return e;
        }
        return ExpressionPtr();
    }

    private fun findAtomExp: ExpressionPtr
    {
        for ( e = expList.all() )
        {
            var k = e.get().kind;
            if ( k == ekAtom )
                return e;
        }
        return ExpressionPtr();
    }

    private fun writeLoc(loc: @Location)
    {
        cout << '(' << loc.startLineNo << ':' << loc.startColNo << " - " << loc.endLineNo << ':' << loc.endColNo << "): ";
    }
}
