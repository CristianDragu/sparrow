import token;
import nodes;
import errorReporter;

concept TokenRange(x)
    if Range(x)
    && -@(x.RetType) == Token
    ;

class[ct] Parser(lexerType, errorReporterType: Type) if TokenRange(#$lexerType)
{
    fun ctor(lexer: @lexerType, errorReporter: @errorReporterType)
    {
        this.lexer ctor lexer;
        this.errorReporter ctor errorReporter;
    }

    fun parse: PrologProgramPtr
    {
        var resptr = PrologProgramPtr.create();
        var res: @PrologProgram = resptr.get();

        while ( !lexer.isEmpty() && lexer().type != tkEof )
        {
            res.predicates.pushBack(parsePredicateClause());
        }

        if ( !res.predicates.isEmpty() )
        {
            res.loc = res.predicates.front().get().loc;
            res.loc copyEnd res.predicates.back().get().loc;
        }

        return resptr;
    }

    private fun parsePredicateClause: PredicateClausePtr
    {
        var resptr = PredicateClausePtr.create();
        var res: @PredicateClause = resptr.get();

        // Read predicate name
        var tok = (lexer++);
        expectToken(tok, tkAtom);
        res.loc = tok.loc;
        res.name = tok.data;

        // Expect '('
        tok = (lexer++);
        expectToken(tok, tkLParen);

        // Parse all arguments, until the parenthesis is closed
        parsePredicateClauseArgs(res);

        // Expect ')'
        tok = (lexer++);
        expectToken(tok, tkRParen);

        // Zero or more predicate calls
        tok = lexer();
        if ( tok.type == tkClause )
        {
            ++lexer;

            // Parse all predicate conditions
            parsePredicateConditions(res);
        }

        // Expect '.'
        tok = (lexer++);
        expectToken(tok, tkDot);

        // Update the end part of the location
        res.loc copyEnd tok.loc;
        return resptr;
    }

    private fun parsePredicateClauseArgs(predClause: @PredicateClause)
    {
        while ( true )
        {
            // Expect a term appropriate for an argument
            var arg = parseTerm();
            var argKind = arg.get().kind;
            if ( argKind != ekVar && argKind != ekAtom && argKind != ekNumber )
                errorReporter.raiseError(arg.get().loc, "Invalid argument found; expected variable, atom or number");
            predClause.args.pushBack(arg);

            // A right parenthesis is our terminator
            var tok = lexer();
            if ( tok.type == tkRParen )
                break;

            // Expect a comma
            expectToken(tok, tkComma);
            ++lexer;
        }
        // The terminator (right parenthesis) is not consumed
    }

    private fun parsePredicateConditions(predClause: @PredicateClause)
    {
        while ( true )
        {
            // Expect a condition expression
            predClause.conditions.pushBack(parseCondition());

            // A dot is our terminator
            var tok = lexer();
            if ( tok.type == tkDot )
                break;

            // Expect a comma
            expectToken(tok, tkComma);
            ++lexer;
        }
        // The terminator (dot) is not consumed
    }

    private fun parseCondition: ConditionPtr
    {
        // A condition can be either a predicate call or an 'is' expression
        // We distinguish between them by the first token

        // If we start with an atom, it must be a predicate call
        var tok = lexer();
        if ( tok.type == tkAtom )
            return parsePredicateCall();

        // an 'is' condition always start with a variable
        if ( tok.type != tkVar )
            invalidToken(tok, String("atom or variable"));
        return parseIsExpression();
    }

    private fun parsePredicateCall: ConditionPtr
    {
        // Read predicate name
        var tok = (lexer++);
        expectToken(tok, tkAtom);
        var loc = tok.loc;
        var name = tok.data;

        // Expect '('
        tok = (lexer++);
        expectToken(tok, tkLParen);

        // Parse all arguments, until the parenthesis is closed
        var args: Vector(ExpressionPtr);
        parsePredicateCallArgs(args);

        // Expect ')'
        tok = (lexer++);
        expectToken(tok, tkRParen);

        // Update the end part of the location
        loc copyEnd tok.loc;
        return mkPredCall(loc, name, args);
    }

    private fun parsePredicateCallArgs(args: @Vector(ExpressionPtr))
    {
        while ( true )
        {
            // Expect an expression
            args.pushBack(parseExpression());

            // A right parenthesis is our terminator
            var tok = lexer();
            if ( tok.type == tkRParen )
                break;

            // Expect a comma
            expectToken(tok, tkComma);
            ++lexer;
        }
        // The terminator (right parenthesis) is not consumed
    }

    private fun parseIsExpression: ConditionPtr
    {
        // Read variable name
        var tok = lexer();
        expectToken(tok, tkVar);
        var varName = parseTerm();
        var loc = tok.loc;

        // Expect 'is'
        tok = (lexer++);
        expectToken(tok, tkIs);

        // Expect an expression
        var exp = parseExpression();

        // Update the end part of the location
        loc copyEnd tok.loc;
        return mkIsExpr(loc, varName, exp);
    }

    private fun parseExpression = parseMulDivExpression();

    private fun parseMulDivExpression: ExpressionPtr
    {
        // Parse left part of the expression
        var left = parsePlusMinusExpression();
        var loc = left.get().loc;

        // Consume all the muls and divs
        while ( true )
        {
            // Is there also a right part?
            var tok = lexer();
            if ( tok.type != tkOpMul && tok.type != tkOpDiv )
                return left;

            // Yes, there is still a right part, so parse it
            ++lexer;
            var right = parsePlusMinusExpression();

            // Combine the left and right into one single expression
            loc copyEnd right.get().loc;
            if ( tok.type == tkOpMul )
                left = mkOpMul(loc, left, right);
            else
                left = mkOpDiv(loc, left, right);
        }
    }

    private fun parsePlusMinusExpression: ExpressionPtr
    {
        // Parse left part of the expression
        var left = parseSimpleExpression();
        var loc = left.get().loc;

        // Consume all the pluses and minuses
        while ( true )
        {
            // Is there also a right part?
            var tok = lexer();
            if ( tok.type != tkOpPlus && tok.type != tkOpMinus )
                return left;

            // Yes, there is still a right part, so parse it
            ++lexer;
            var right = parseSimpleExpression();

            // Combine the left and right into one single expression
            loc copyEnd right.get().loc;
            if ( tok.type == tkOpPlus )
                left = mkOpPlus(loc, left, right);
            else
                left = mkOpMinus(loc, left, right);
        }
    }

    private fun parseSimpleExpression: ExpressionPtr
    {
        // Accept expression in parenthesis
        var tok = lexer();
        if ( tok.type == tkLParen )
        {
            ++lexer;
            var res = parseExpression();

            // Expect ')'
            tok = (lexer++);
            expectToken(tok, tkRParen);

            return res;
        }

        // Otherwise this must be a term
        return parseTerm();
    }

    private fun parseTerm: ExpressionPtr
    {
        var tok = (lexer++);
        if ( tok.type == tkAtom )
            return mkAtom(tok.loc, tok.data);
        else if ( tok.type == tkNumber )
            return mkNumber(tok.loc, tok.data);
        else if ( tok.type == tkVar )
            return mkVar(tok.loc, tok.data);
        else if ( tok.type == tkAntiQuote )
            return mkAntiQuote(tok.loc, tok.data);
        else
            invalidToken(tok, String("atom, variable, number or antiquote"));
        return ExpressionPtr();
    }


    private fun expectToken(tok: @Token, expected: Int)
    {
        if ( tok.type != expected )
            invalidToken(tok, tokenTypeToString(expected));
    }

    private fun invalidToken(tok: @Token, expected: @String)
    {
        var msg: String = String("Invalid token found: ") + tokenTypeToString(tok.type) + String("; expected: ") + expected;
        errorReporter.raiseError(tok.loc, msg.asStringRef());
    }

    private var lexer: lexerType;
    private var errorReporter: errorReporterType;
}
fun[ct] mkParser(lexer: @TokenRange, errorReporter: @AnyType) = Parser(-@typeOf(lexer), -@typeOf(errorReporter))(lexer, errorReporter);
