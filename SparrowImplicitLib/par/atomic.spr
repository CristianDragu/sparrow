module par.atomic;

import config;
import "atomic.llvm";

[initCtor] class Atomic(T: Type) if T == Int {
    using _IsAtomicType = true;
    using ValueType = Int;

    //! The value for this atomic
    var _value: ValueType;
}

[initCtor] class Atomic(T: Type) if T == Long {
    using _IsAtomicType = true;
    using ValueType = Long;

    //! The value for this atomic
    var _value: ValueType;
}

class Atomic(T: Type) if T != Int && T != Long && sizeOf(T) <= 8 {
    using _IsAtomicType = true;

    using ValueType = T;

    using _UnderlyingType = Impl.AtomicTypeTraits(T)._UnderlyingType;
    using _BaseAtomicType = Impl.AtomicTypeTraits(T)._BaseAtomicType;

    fun _toUnderlying(val: T): _UnderlyingType {
        var res: _UnderlyingType;
        reinterpretCast(@T, res) = val;
        return res;
    }
    fun _fromUnderlying(val: _UnderlyingType): T {
        var res: T;
        res = reinterpretCast(@T, val);
        return res;
    }
    fun _baseAtomic: @_BaseAtomicType = reinterpretCast(@_BaseAtomicType, this);

    //! The value for this atomic
    var _value: _UnderlyingType;

    fun ctor(v: ValueType) {
        _value construct _toUnderlying(v);
    }
}

using AtomicInt = Atomic(Int);
using AtomicLong = Atomic(Long);

concept AtomicType(x) if x._IsAtomicType;
concept AtomicInteger(x) if x._IsAtomicType && Integer(#$x.ValueType);
concept NonStdAtomicType(x) if x._IsAtomicType && x.ValueType != x._UnderlyingType;

fun =(lhs: @AtomicType, rhs: AtomicType) { lhs store (rhs load); }

//! Converts a regular value to an atomic value
//! Returns the reference to the same memory address
//! Used to make it possible to use atomic operations
fun asAtomic(val: @AnyType): @Atomic(typeOf(val))
    = reinterpretCast(@Atomic(typeOf(val)), val)
    if sizeOf(val) == sizeOf(Atomic(typeOf(val)));

//! Loads the value from an atomic; returns a non-atomic value
[native("_Atomic_load32")] fun load(x: @AtomicInt): Int;
[native("_Atomic_load64")] fun load(x: @AtomicLong): Long;
fun load(x: @NonStdAtomicType): x.ValueType
    = x._fromUnderlying(x._baseAtomic load);

//! Stores a value inside the given atomic
[native("_Atomic_store32")] fun store(x: @AtomicInt, newVal: Int);
[native("_Atomic_store64")] fun store(x: @AtomicLong, newVal: Long);
fun store(x: @NonStdAtomicType, newVal: AnyType)
    { x._baseAtomic store x._toUnderlying(newVal); }

//! Stores a value inside the given atomic, using the assignment operator
fun =(x: @AtomicType, val: AnyType) = store(x, val);

//! Fetches the current value of an atomic, and stores a new value in the atomic
[native("_Atomic_fetchAndStore32")] fun fetchAndStore(x: @AtomicInt, newVal: Int): Int;
[native("_Atomic_fetchAndStore64")] fun fetchAndStore(x: @AtomicLong, newVal: Long): Long;
fun fetchAndStore(x: @NonStdAtomicType, newVal: AnyType): x.ValueType
    = x._fromUnderlying(x._baseAtomic fetchAndStore x._toUnderlying(newVal));

//! Compare the atomic value with the given comparand; if equal store 'newVal' and return true; if not equal returns false
[native("_Atomic_compareAndSwap32")] fun compareAndSwap(x: @AtomicInt, newVal, comparand: Int): Bool;
[native("_Atomic_compareAndSwap64")] fun compareAndSwap(x: @AtomicLong, newVal, comparand: Long): Bool;
fun compareAndSwap(x: @NonStdAtomicType, newVal, comparand: AnyType): Bool
    = compareAndSwap(x._baseAtomic, x._toUnderlying(newVal), x._toUnderlying(comparand));

//! Fetch the value of the atomic, and then add the given value to it
[native("_Atomic_fetchAndAdd32")] fun fetchAndAdd(x: @AtomicInt, val: Int): Int;
[native("_Atomic_fetchAndAdd64")] fun fetchAndAdd(x: @AtomicLong, val: Long): Long;
fun fetchAndAdd(x: @NonStdAtomicType, val: AnyType): x.ValueType
    = x._fromUnderlying(x._baseAtomic fetchAndAdd x._toUnderlying(val))
    if Integer(#$x.ValueType);

//! Fetch the value of the atomic and the increment it
fun fetchAndIncrement(x: @AtomicInteger) = x fetchAndAdd 1;
//! Fetch the value of the atomic and the decrement it
fun fetchAndDecrement(x: @AtomicInteger) = x fetchAndAdd -1;

//! Adds the given value to the atomic
fun += (x: @AtomicInteger, val: AnyType) { x fetchAndAdd val; }
//! Subtracts the given value to the atomic
fun -= (x: @AtomicInteger, val: AnyType) { x fetchAndAdd (-val); }

//! Increment the atomic value; returns the new value
fun pre_++(x: @AtomicInteger): x.ValueType = (x fetchAndIncrement)+1;
//! Increment the atomic value; returns the old value
fun post_++(x: @AtomicInteger): x.ValueType = (x fetchAndIncrement);

//! Decrement the atomic value; returns the new value
fun pre_--(x: @AtomicInteger): x.ValueType = (x fetchAndDecrement)-1;
//! Decrement the atomic value; returns the old value
fun post_--(x: @AtomicInteger): x.ValueType = (x fetchAndDecrement);

package Impl {
    class AtomicTypeTraits(T: Type) if sizeOf(T) <= 4 {
        using _UnderlyingType = Int;
        using _BaseAtomicType = Atomic(Int);
    }
    class AtomicTypeTraits(T: Type) if sizeOf(T) == 8 {
        using _UnderlyingType = Long;
        using _BaseAtomicType = Atomic(Long);
    }

    fun packAtomic(x: @NonStdAtomicType): @AtomicTypeTraits(x.ValueType)
        = reinterpretCast(@AtomicTypeTraits(x.ValueType), x);

    fun pack(x: @NonStdAtomicType, val: AnyType): AtomicTypeTraits(x.ValueType)._UnderlyingType {
        var res: AtomicTypeTraits(x.ValueType)._UnderlyingType;
        reinterpretCast(@AtomicTypeTraits(x.ValueType)._UnderlyingType, res) = val;
        return res;
    }
    fun unpack(x: @NonStdAtomicType, val: AnyType): x.ValueType {
        var res: x.ValueType;
        res = reinterpretCast(@AtomicTypeTraits(x.ValueType)._UnderlyingType, val);
        return res;
    }
}