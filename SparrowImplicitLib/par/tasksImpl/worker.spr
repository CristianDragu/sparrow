module par.tasksImpl.worker;

//import debug(Tracer);
import taskSystem(TaskSystem, getInitTaskSystem, createWorker);
import taskQueue(TaskQueue);
import par.tls, par.semaphore, par.atomic;
import par.semaphore;
import par.atomic;

//! Class used to keep count of the number of threads that go idle
class IdleCounter {
    var cnt: Int Atomic;
}

fun goIdle(c: @IdleCounter) { c.cnt++; }
fun wakeUp(c: @IdleCounter) { c.cnt--; }
fun hasIdle(c: @IdleCounter) = (c.cnt load) > 0;
fun numIdleWorkers(c: @IdleCounter) = c.cnt load;


//! A worker class describes a thread that is doing some work on our task system
class Worker
    var taskSystem: @TaskSystem;
    var work: TaskQueue;
    var workerId: Int;
    var curDepth: Int;

    //! The global counter that keeps track on how many internal threads are idle
    var _idleCounter: @IdleCounter;
    //! Non-zero if this thread has work to do
    var _isBusy: Int Atomic;
    //! Semaphore used when sleeping
    var _waitSem: Semaphore;

fun ctor(this: @Worker, taskSystem: @TaskSystem, workerId: Int, idleCounter: @IdleCounter)
    this.taskSystem := taskSystem;
    this.workerId = workerId;
    this.curDepth = 0;
    this._idleCounter := idleCounter;
    this._isBusy store 0;
    this._waitSem ctor 0;

fun dtor(this: @Worker) {}

using WorkerPtr = Worker Ptr;

var _tlsWorker: Tls(@Worker);

fun localWorker: Worker Ptr {
    var worker: Worker Ptr = _tlsWorker get;
    if ( worker isNull ) {
        worker = (getInitTaskSystem createWorker);
        _tlsWorker = worker.get();
    }
    return worker;
}

fun setLocalWorker(worker: Worker Ptr) {
    _tlsWorker = worker.get();
}

fun goIdle(w: @Worker) {
    if ( compareAndSwap(w._isBusy, 0, 1) ) {
        // Change the global counter
        w._idleCounter goIdle;

        [ct] if ( isValidAndTrue(traceCalls) )
            Tracer(w) << "going idle; we have " << (w._idleCounter.cnt load) << " idle workers" << endl;

        // Actually wake up the thread
        w._waitSem acquire;

        [ct] if ( isValidAndTrue(traceCalls) )
            Tracer(w) << "woke up; we have " << (w._idleCounter.cnt load) << " idle workers" << endl;
    }
}

fun tryWakeUp(w: @Worker): Bool {
    //[ct] if ( isValidAndTrue(traceCalls) )
    //    Tracer(w) << "trying to wake up; _isBusy= " << (w._isBusy load) << endl;
    // If the thread was now busy, and we can set it to busy,
    // release the semaphore to wake up the thread
    if ( compareAndSwap(w._isBusy, 1, 0) ) {
        [ct] if ( isValidAndTrue(traceCalls) )
            Tracer(w) << "waking up; we have " << (w._idleCounter.cnt load) << " idle workers" << endl;

        w._waitSem release;

        // Make sure the counter will not count us anymore
        w._idleCounter wakeUp;
        return true;
    }
    return false;
}
