module par.thread;

import config;
import std.newDelete;
//import assert;

//! Datatype describing a thread ID
class ThreadId {
    var id: ULong;

    fun ctor(id: ULong) {
        this.id ctor id;
    }
    fun ctor(h: NativeThreadHandle) {
        this.id ctor;
        Impl.pthread_threadid_np(h, id);
    }
}
fun >>(tid: @ThreadId, os: @OutStream)
{
    os << tid.id;
}



class Thread
{
    var _handle: NativeThreadHandle;

    //! Construct the Thread with a functor
    //! The functor can have state, not just executing code
    fun ctor(f: AnyType) if isValid(f()) {
        _handle = Impl.startThread(f);
    }
    //! Destructor. Detatch the thread if joinable
    fun dtor {
        if ( this isJoinable ) this detach;
    }

    //! Cannot copy construct or assign a Thread object
    fun[private] ctor(other: @Thread);
    fun[private] =(other: @Thread);

}

fun >>(t: @Thread, os: @OutStream)
{
    if ( _handle != NativeThreadHandle() )
        os << "Thread(" << _handle << ")";
    else
        os << "Thread(null)";
}

//! Swap the content of two Thread objects
//fun swap(lhs, rhs: @Thread) {
//    var tmp = lhs._handle swap rhs._handle;
//}

//! Test if the thread is joinable (e.g., still active);
fun isJoinable(t: Thread) = t._handle != NativeThreadHandle();

//! Wait for the thread to finish
//! This shouldn't be called from the same thread
fun join(t: @Thread) {
    //assert(t isJoinable);
    //assert((t getId) !== curThreadId);
    var res = Impl.pthread_join(t._handle, null);
    //assert(res == 0);
    t._handle = NativeThreadHandle();
}

//! Detaches the given thread object from the actual thread of execution
//! The actual thread may continue to execute after this call
fun detach(t: @Thread) {
    //assert(t isJoinable);
    var res = Impl.pthread_detach(t._handle);
    //assert(res == 0);
    t._handle = NativeThreadHandle();
}

//! Get the ID of the given thread
fun getId(t: Thread): ThreadId = ThreadId(t._handle);

//! Get the native handle of the given thread
fun getNativeHandle(t: Thread): NativeThreadHandle = t._handle;


//! Get the current thread id
fun curThreadId: ThreadId = ThreadId(NativeThreadHandle());

//! Tries to suspend the current thread to let other threads execute
fun yield = Impl.sched_yield();

//! Sleep the given amount of milliseconds
fun sleep(durMs: UInt) {
    var req = Impl.TimeSpec(durMs/1000, (durMs%1000)*1000000);
    var rem = Impl.TimeSpec(0, 0);
    Impl.nanosleep(req, rem);
}

package Impl {
    using ThreadFun = FunctionPtr(@Byte rt, @Byte rt);

    class[initCtor] Closure(FType: Type) {
        var f: FType;

        fun[static] threadFun(arg: @Byte): @Byte {
            var self: @Closure = reinterpretCast(@Closure, arg);
            self.f();
            self delete;
            return null;
        }

        fun[static] getFunPtr: ThreadFun = \threadFun;
    }
    fun startThread(f: AnyType): NativeThreadHandle {
        using ClosureType = Closure(typeOf(f));
        var closurePtr: @ClosureType = new(ClosureType, f);
        var handle: NativeThreadHandle;
        var arg: @Byte = reinterpretCast(@Byte, closurePtr);
        var status = pthread_create(handle, null, ClosureType.getFunPtr, arg);
        return handle;
    }

    class[initCtor] TimeSpec { var tv_sec, tv_nsec: Long; }

    fun[native("pthread_create")] pthread_create(handle: @NativeThreadHandle, attr: @Byte, f: ThreadFun, arg: @Byte): Int;
    fun[native("pthread_join")] pthread_join(handle: NativeThreadHandle, valPtr: @ @Byte): Int;
    fun[native("pthread_detach")] pthread_detach(handle: NativeThreadHandle): Int;
    fun[native("pthread_self")] pthread_self: NativeThreadHandle;

    fun[native("pthread_threadid_np")] pthread_threadid_np(t: NativeThreadHandle, res: @ULong): Int;

    fun[native("sched_yield")] sched_yield;
    fun[native("nanosleep")] nanosleep(req, rem: @TimeSpec): Int;
}

