module par.tls;

import config;
import assert;

//! A class that can hold some data into a thread-local storage
//! The size of the data must be less or equal to the size of a reference
//! For references, we would delete the pointer when the thread exits
class Tls(T: Type) if TypeOp.isRef(T) {
    private var tls: Impl.TlsCommon;    

    using ValueType = T;

    fun ctor {
        tls.ctor(\destructPtr);
    }

    fun =(other: @Tls) {
        tls setValue (other.tls getValue);
    }
    fun =(val: T) {
        tls setValue reinterpretCast(@Byte, val);
    }


    fun get: T {
        return reinterpretCast(T, tls getValue);
    }

    private fun[static] destructPtr(p: @Byte): Null {
        delete(reinterpretCast(T, p));
        return Null();
    }
}
class Tls(T: Type) if !TypeOp.isRef(T) && sizeOf(T) <= sizeOf(@Byte) {
    private var tls: Impl.TlsCommon;    

    using ValueType = T;

    fun =(other: @Tls) {
        tls setValue (other.tls getValue);
    }
    fun =(val: T) {
        tls setValue reinterpretCast(@ @Byte, val);
    }

    fun get: T {
        return reinterpretCast(@ @T, tls getValue);
    }
}


package Impl {
    using DtorFun = FunctionPtr(Null rt, @Byte rt);

    class TlsCommon {
        var key: KeyT;

        fun ctor {
            pthread_key_create(key, DtorFun());
        }
        fun ctor(dtorFun: DtorFun) {
            pthread_key_create(key, dtorFun);
        }

        fun dtor {
            pthread_key_delete(key);
        }
    }

    fun setValue(tls: @TlsCommon, val: @Byte) { pthread_setspecific(tls.key, val); }
    fun getValue(tls: @TlsCommon): @Byte = pthread_getspecific(tls.key);

    using KeyT = ULong;

    fun[native("pthread_key_create")] pthread_key_create(k: @KeyT, dtorFun: DtorFun): Int;
    fun[native("pthread_key_delete")] pthread_key_delete(k: KeyT): Int;
    fun[native("pthread_setspecific")] pthread_setspecific(k: KeyT, val: @Byte): Int;
    fun[native("pthread_getspecific")] pthread_getspecific(k: KeyT): @Byte;
}