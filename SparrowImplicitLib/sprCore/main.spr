import = basicDecls(Char, Byte, Int, SizeType, DiffType, StringRef, Null, typeOf, reinterpretCast, sizeOf, ptrAdd, ptrSub, ptrDiff, *);

/// Range type that returns StringRef objects referring to program arguments
class MainParameters
{
    using RetType = StringRef;

    fun isEmpty                     = size == 0;
    fun size: SizeType              = end - begin;

    fun front: RetType              = begin toStringRef;
    fun back: RetType               = end - 1 toStringRef;
    fun () : RetType                = begin toStringRef;
    fun ()(n: SizeType): RetType    = begin + n toStringRef;

    fun popFront                    { begin = (begin + 1); }
    fun popBack                     { end = (end - 1); }
    fun popFront(n: SizeType)       { begin = (begin + n); }
    fun popBack(n: SizeType)        { end = (end - n); }

    private fun ctor(argc: Int, argv: @ @Char)
    {
        this.begin = (Impl.fromArgvPtr(argv));
        this.end = (this.begin + argc);
    }

    private var begin, end: Impl.CStrPtr;
}

private package Impl {
    class CStr { var cstr: @Char; }
    class[initCtor] CStrPtr { var ptr: @CStr; }

    fun fromArgvPtr(argv: @ @Char): CStrPtr = CStrPtr(reinterpretCast(@CStr, argv));
    fun fromBytePtr(p: @Byte): CStrPtr = CStrPtr(reinterpretCast(@CStr, p));
    fun toBytePtr(p: CStrPtr): @Byte = reinterpretCast(@Byte, p.ptr);
    fun +(p: CStrPtr, n: SizeType): CStrPtr = fromBytePtr( ptrAdd(toBytePtr(p), n*(sizeOf(CStrPtr))) );
    fun -(p: CStrPtr, n: SizeType): CStrPtr = fromBytePtr( ptrSub(toBytePtr(p), n*(sizeOf(CStrPtr))) );
    fun -(p, q: CStrPtr): SizeType = ptrDiff(toBytePtr(p), toBytePtr(q)) / (sizeOf(CStrPtr));

    fun toStringRef(p: CStrPtr): StringRef = StringRef.fromCString(p.ptr.cstr);
}

var programArgs: MainParameters;

// To be defined by the application:
fun sprMain();

fun[native("main"), noInline] main(argc: Int, args: @ @Char): Int
{
    programArgs.ctor(argc, args);
    sprMain();
    return 0;
}
