module std.set;

import hashTable(HashTable);
import typeTraits(DefaultTypeTraits);

class Set(keyType: Type, traitsType: Type = DefaultTypeTraits)
{
    using KeyType = keyType;
    using ValueType = keyType;
    using TraitsType = traitsType;
    using _ImplTable = HashTable(keyType, ValueType, Impl.Identity(keyType), traitsType);
    using RangeType = _ImplTable.RangeType;

    var _hashTable: _ImplTable;

    fun ctor(n: SizeType)                                       { _hashTable.ctor(n, TraitsType()); }
    fun ctor(n: SizeType, traits: traitsType)                   { _hashTable.ctor(n, traits); }
    fun ctor(range: Range)                                      { _hashTable.ctor(range, 0, TraitsType()); }
    fun ctor(range: Range, n: SizeType)                         { _hashTable.ctor(range, n, HashType(), CompareType()); }
    fun ctor(range: Range, n: SizeType, traits: traitsType)     { _hashTable.ctor(range, n, traits); }
    fun ctor(other: @Set)                                       { this._hashTable ctor other._hashTable; }

    fun traits                                  = _hashTable.traits;

    fun size                                    = _hashTable.size;
    fun isEmpty                                 = (_hashTable.size == 0);
    fun bucketCount                             = _hashTable.bucketCount;
    fun reserve(n: SizeType)                    { _hashTable.reserve(n); }

    fun all                                     = _hashTable.all();

    fun insert(value: @ValueType)               = _hashTable.insert(value);
    fun insert(range: Range)                    { _hashTable.insert(range); }
    fun remove(key: @KeyType)                   { _hashTable.remove(key); }
    fun remove(range: RangeType)                { _hashTable.remove(range); }

    fun +=(value: @ValueType)                   { insert(value); }
    fun +=(range: Range)                        { insert(range); }
    fun -=(key: @KeyType)                       { remove(key); }
    fun -=(range: RangeType)                    { remove(range); }

    fun contains(key: @KeyType)                 = _hashTable.contains(key);
    fun count(key: @KeyType)                    = _hashTable.count(key);
    fun equalRange(key: @KeyType)               = _hashTable.equalRange(key);
    fun find(key: @KeyType)                     = _hashTable.find(key);

    fun clear()                                 { _hashTable.clear(); }

    fun swap(other: @Set)                       { this._hashTable swap other._hashTable; }

    fun >> (os: @OutStream)                     { _hashTable.>>(os); }
}

package Impl
{
    class Identity(type: Type)
    {
        fun () (val: type): type = val;
    }
}
