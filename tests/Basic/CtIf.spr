//!! -t "SparrowImplicitLib.spr" -fno-main

[native("writeLnDouble"), autoCt] fun writeLnDoubleCt(x: Double);

// Function that can be called only with compile-time arguments
[ct] fun ctTest(x: Double)
{
    writeLnDoubleCt(x);
}

class TestClass
{
    [ct,static] fun ctMethod(n: Int): Int
    {
        return add(n,1000);
    }

    [ct] if ( true )
        fun f { writeLn("f.1"); }
    else
        fun f { writeLn("f.2"); }

    [ct] if ( false )
        fun g { writeLn("g.1"); }
    else
        fun g { writeLn("g.2"); }
}

class Gen(t: Type)
{
    [ct] if ( t == Int )
        fun print { writeLn("Int"); }
    else [ct] if ( t == Double )
        fun print { writeLn("Double"); }
    else
        fun print { writeLn("unknown type"); }
}

[native("test")] fun test(n: Int)
{
    if ( true )
        ctTest(1.0);
    else
        ctTest(2.0);

    [ct] if ( true )
        ctTest(11.0);
    else
        ctTest(12.0);
    [ct] if ( false )
        ctTest(21.0);
    else
        ctTest(22.0);

    [ct] if ( true )
    {
        writeLn("ok");
    }
    else
    {
        writeLn("fail");
        some error here;
    }

    var x: TestClass;
    x.f();
    x.g();

    var gi: Gen(Int);
    var gd: Gen(Double);
    var gc: Gen(Char);
    gi.print();
    gd.print();
    gc.print();
}

/*<<<Running()
ok
f.1
g.2
Int
Double
unknown type
>>>*/