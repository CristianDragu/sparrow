//!!

import std.ranges;
import std.vector;

class[ct] MyRange
{
    using RetType = Int;

    fun ctor(min, max: Int)
    {
        this._min ctor min;
        this._max ctor max;
    }

    fun front = _min;
    fun popFront { _min += 1; }
    fun isEmpty = _min > _max;

    var _min, _max: Int;
}

fun test1()
{
    for[ct] ( i = MyRange(0, 4))
    {
        cout << ctEval(i) << endl;
    }
}

fun[ct] fact(n: Int): Int
{
    if ( n < 2 )
        return 1;
    else
        return n*fact(n-1);
}

fun test2()
{
    for[ct] ( i = 0..5 )
    {
        cout << ctEval(fact(i)) << endl;
    }
}

fun[ct] createVector(min, max: Int): Vector(Int)
{
    var v = Vector(Int)(min..max);
    return v;
}

fun test3()
{
    var[ct] v = createVector(10, 15);

    for[ct] ( el = v.all() )
    {
        cout << ctEval(el) << endl;
    }
}

fun sprMain
{
    var x = 10;
    var y = 2;
    var[ct] yy = 2;

    test1();
    cout << "---" << endl;
    test2();
    cout << "---" << endl;
    test3();
}

/*<<<Running()
0
1
2
3
4
---
1
1
2
6
24
---
10
11
12
13
14
>>>*/
